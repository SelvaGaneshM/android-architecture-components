apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"

android {
  compileSdkVersion deps.build_versions.compile_sdk

  defaultConfig {
    minSdkVersion deps.build_versions.min_sdk
    targetSdkVersion deps.build_versions.target_sdk
    versionCode 1
    versionName "1.0"
    applicationId "co.windly.aac"
    multiDexEnabled true
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  buildTypes {
    debug {
      minifyEnabled false
      shrinkResources false
      useProguard false
    }
    release {
      minifyEnabled true
      shrinkResources true
      useProguard true
      files { rootProject.file("settings/proguard").listFiles() }.each {
        proguardFile it
      }
    }
  }

  buildTypes.each {
    it.buildConfigField "String", "API_URL", ApiUrl
  }

  dataBinding {
    enabled = true
  }

  packagingOptions {
    exclude "META-INF/rxjava.properties"
  }
}

dependencies {
  implementation deps.kotlin.sdtlib

  // Dex.
  implementation deps.support.multidex

  // UI.
  implementation deps.support.v7
  implementation deps.support.card_view_v7
  implementation deps.support.design
  implementation deps.support.constraint
  implementation deps.support.recycler_view

  // Dependency injection.
  implementation deps.dagger.runtime
  implementation deps.dagger.android_support
  kapt deps.dagger.compiler
  kapt deps.dagger.processor
  compileOnly deps.javax

  // Reactive Extensions.
  implementation deps.rx.java
  implementation deps.rx.kotlin
  implementation deps.rx.android

  // Networking.
  implementation deps.retrofit.retrofit
  implementation deps.retrofit.rxjava2
  implementation deps.retrofit.jackson
  implementation deps.retrofit.logging

  // Utils.
  implementation deps.glide
  kapt deps.glide_compiler
  implementation deps.jraf_prefs
  kapt deps.jraf_prefs_compiler
  implementation deps.joda_time
  implementation deps.commons_lang

  // Mapping.
  implementation deps.model_mapper

  // Data Binding.
  kapt deps.databinding

  // Lifecycle.
  implementation deps.lifecycle.runtime
  implementation deps.lifecycle.extensions
  implementation deps.lifecycle.reactive_streams

  // Room Database.
  implementation deps.room.runtime
  implementation deps.room.rxjava2
  kapt deps.room.compiler

  // Paging.
  implementation deps.paging

  // Debug.
  implementation deps.stetho
  implementation deps.timber

  // Instrumented & integration tests.
  androidTestImplementation deps.arch_core.testing
  androidTestImplementation deps.espresso.core
  androidTestImplementation deps.atsl.runner
  androidTestImplementation deps.atsl.rules
  androidTestImplementation deps.test.truth
}

android.testVariants.all { variant ->
  variant.getCompileConfiguration().exclude group: "com.android.support", module: "support-annotations"
  variant.getRuntimeConfiguration().exclude group: "com.android.support", module: "support-annotations"
}
